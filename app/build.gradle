plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 36

    namespace = 'org.who.gdhcnvalidator'

    defaultConfig {
        applicationId "org.who.gdhcnvalidator"
        minSdkVersion 26
        targetSdkVersion 36
        versionCode 22
        versionName "0.1.0"

        buildConfigField "String", "OPENID_CLIENT_ID", "\"verifier-app\""
        buildConfigField "String", "OPENID_REDIRECT_URI", "\"${applicationId}:/redirect\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders['appAuthRedirectScheme'] = "${applicationId}"
    }

    buildTypes {
        debug {
            buildConfigField "String", "OPENID_SERVER_URL", "\"http://${getDevHost()}:8080/auth/realms/master/\""
            manifestPlaceholders['usesCleartextTraffic'] = true
        }
        release {
            buildConfigField "String", "OPENID_SERVER_URL", "null"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled = true
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding = true
        buildConfig = true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE.md', 'META-INF/NOTICE.md']
            pickFirsts += ['META-INF/ASL-2.0.txt', 'META-INF/LGPL-3.0.txt', 'license.html', 'readme.html', 'META-INF/sun-jaxb.episode']
        }
    }
    sourceSets {
        // Shares Resources directory from test to Instrumented tests
        androidTest {
            resources.srcDirs += ['src/main/assets']
        }
        // Copy App asset files to the test resources.
        test {
            resources.srcDirs += ['src/main/assets']
        }
    }
    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
    }
    buildToolsVersion '36.1.0'
}

configurations.configureEach {
    exclude module: "xpp3"
    exclude module: "xpp3_min"
    exclude module: "xmlpull"
    exclude module: "javax.json"
    exclude module: "jcl-over-slf4j"
    exclude group: "org.apache.httpcomponents"
    exclude module: "antlr4"

    resolutionStrategy {
        force 'com.fasterxml.jackson.core:jackson-core:2.17.2'
        force 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
        force 'com.fasterxml.jackson.core:jackson-annotations:2.17.2'
        force 'com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.17.2'
        force 'com.fasterxml.jackson.datatype:jackson-datatype-jsr:2.17.2'
        force 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.2'
        force 'com.fasterxml.jackson:jackson-bom:2.17.2'
        force 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.17.2'

        force 'com.google.guava:guava:33.3.0-android'

        force 'ca.uhn.hapi.fhir:hapi-fhir-structures-r4:6.8.3'
        force 'ca.uhn.hapi.fhir:hapi-fhir-validation-resources-r4:6.8.3'
        force 'ca.uhn.hapi.fhir:hapi-fhir-validation:6.8.3'
        force 'ca.uhn.hapi.fhir:hapi-fhir-caching-guava:6.8.3'

        force 'ca.uhn.hapi.fhir:hapi-fhir-base:6.8.3'
        force 'ca.uhn.hapi.fhir:hapi-fhir-client:6.8.3'
        force 'ca.uhn.hapi.fhir:hapi-fhir-structures-dstu2:6.8.3'
        force 'ca.uhn.hapi.fhir:hapi-fhir-structures-dstu3:6.8.3'
        force 'ca.uhn.hapi.fhir:hapi-fhir-structures-r5:6.8.3'
        force 'ca.uhn.hapi.fhir:hapi-fhir-validation-resources-dstu3:6.8.3'
        force 'ca.uhn.hapi.fhir:hapi-fhir-validation-resources-r5:6.8.3'

        force 'ca.uhn.hapi.fhir:org.hl7.fhir.convertors:6.0.22.2'
        force 'ca.uhn.hapi.fhir:org.hl7.fhir.dstu2:6.0.22.2'
        force 'ca.uhn.hapi.fhir:org.hl7.fhir.dstu3:6.0.22.2'
        force 'ca.uhn.hapi.fhir:org.hl7.fhir.dstu2016may:6.0.22.2'
        force 'ca.uhn.hapi.fhir:org.hl7.fhir.r4:6.0.22.2'
        force 'ca.uhn.hapi.fhir:org.hl7.fhir.r4b:6.0.22.2'
        force 'ca.uhn.hapi.fhir:org.hl7.fhir.r5:6.0.22.2'
        force 'ca.uhn.hapi.fhir:org.hl7.fhir.utilities:6.0.22.2'
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.2'

    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.9.6'
    implementation 'androidx.navigation:navigation-ui-ktx:2.9.6'
    implementation "androidx.cardview:cardview:1.0.0"

    // Necessary to open a camera.
    implementation 'androidx.camera:camera-camera2:1.3.4'
    implementation 'androidx.camera:camera-lifecycle:1.3.4'
    implementation 'androidx.camera:camera-view:1.3.4'

    implementation project(':verify')
    implementation project(":trust")
    implementation project(":trust-pathcheck")
    implementation project(":trust-didweb")

    // Necessary to unpack DIVOC QR Codes
    // LD-signatures requires Jakarta JSON and HTTP Client
    implementation (group: 'org.glassfish', name: 'jakarta.json', version: '2.0.1')

    // Required for the Online update of a Credential
    implementation 'net.openid:appauth:0.11.1'

    // Necessary to find a QR Code in camera preview frame
    implementation 'com.google.mlkit:barcode-scanning:17.3.0'

    // Necessary to Load Jackson-based FHIR Libraries (FHIR Helper, Model Info, and CQL Rules)
    implementation group: 'xerces', name: 'xercesImpl', version: '2.12.2'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.17.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.17.2'
    implementation 'com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.17.2'

    // Necessary to call JSON APIs (Download the Trust Registry)
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.17.2'

    // Necessary to Load CQL Engine's evaluation classes generated with JAXB (Versions must match)
    implementation 'org.jvnet.jaxb2_commons:jaxb2-basics:0.12.0'

    // Necessary to Convert the QR into the FHIR Composite
    implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-r4:6.8.0'

    // FHIR Engine
    implementation ('com.google.android.fhir:engine:1.0.0')
    implementation ('com.google.android.fhir:knowledge:0.1.0-beta01')
    // CQL Engine
    implementation ('com.google.android.fhir:workflow:0.1.0-alpha04')

    implementation 'org.opencds.cqf.fhir:cqf-fhir-cr:3.0.0-PRE9'
    implementation 'org.opencds.cqf.fhir:cqf-fhir-jackson:3.0.0-PRE9'
    implementation 'org.opencds.cqf.fhir:cqf-fhir-utility:3.0.0-PRE9'

    // Tests outside Android can process CQL.
    // Good to test new rules without having to convert to JSON
    // testImplementation group: 'info.cqframework', name: 'cql-to-elm', version: '2.2.0'

    testImplementation 'androidx.test.ext:junit-ktx:1.2.1'
    testImplementation 'androidx.test:core:1.6.1'
    testImplementation 'androidx.test:core-ktx:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-inline:4.8.1'
    testImplementation project(':test-resources')

    androidTestImplementation 'androidx.test:core-ktx:1.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation project(':test-resources')
}

/**
 * Find the first LAN address of the development machine.
 */
static def getDevHost() {
    def defaultDevHost = InetAddress.getLocalHost().getCanonicalHostName()

    return NetworkInterface.getNetworkInterfaces().findResult(defaultDevHost) {
        def ifOk = it.isUp() &&
                !it.isVirtual() &&
                !it.isPointToPoint() &&
                !it.isLoopback() &&
                !it.getName().startsWith("br-") //

        if (ifOk)
            return it.getInetAddresses().find {
                it instanceof Inet4Address && !it.isLoopbackAddress()
            }
        else
            return null
    }
}